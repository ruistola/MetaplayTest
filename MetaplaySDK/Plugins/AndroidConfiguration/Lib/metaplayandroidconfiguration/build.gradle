plugins {
    id 'com.android.library'
}

android {
    namespace 'io.metaplay.android.configuration'
    compileSdk 33

    defaultConfig {
        minSdk 1
        targetSdk 33
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        release {
            minifyEnabled true
            debuggable false
            // Trim and obfuscate this library when this library is compiled into AAR
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            // Pass on the same preserve-rules to the game build when AAR is used there
            consumerProguardFiles 'proguard-rules.pro'
        }
    }
}

if (System.getenv('UNITY_EDITOR_HOME') == null) {
    throw new GradleException("UNITY_EDITOR_HOME must be defined. Check the README.")
}

dependencies {
    compileOnly files(System.getenv('UNITY_EDITOR_HOME') + '/Editor/Data/PlaybackEngines/AndroidPlayer/Variations/il2cpp/Development/Classes/classes.jar')
}

tasks.register('buildLib') {
    dependsOn "assembleRelease"
    doLast {
        File binFolder = new File(projectDir.getParentFile().getParentFile(), 'BuiltLibrary')
        binFolder.mkdirs()
        java.nio.file.Files.copy(new File(projectDir, 'build/outputs/aar/metaplayandroidconfiguration-release.aar').toPath(), new File(binFolder, 'metaplayandroidconfiguration-release.aar').toPath(), java.nio.file.StandardCopyOption.REPLACE_EXISTING)
    }
}
